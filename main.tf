# Create an s3 bucket to store the files for hosting a website. S3 bucket name should be globally unique
resource "aws_s3_bucket" "mybucket" {
  bucket = var.bucketname
}

# Define the bucket ownership. Everything inside this bucket is owned by the bucket owner itself
resource "aws_s3_bucket_ownership_controls" "example" {
  bucket = aws_s3_bucket.mybucket.id

  rule {
    object_ownership = "BucketOwnerPreferred"
  }
}

# By default, new buckets and objects don't allow public access. To Make the bucket public use the code below
resource "aws_s3_bucket_public_access_block" "example" {
  bucket = aws_s3_bucket.mybucket.id

  block_public_acls       = false
  block_public_policy     = false
  ignore_public_acls      = false
  restrict_public_buckets = false
}

# Following code explicitly disables the default S3 bucket security settings. 
# All bucket objects become publicly exposed since we are hosting a static website which is publicly accessible.
resource "aws_s3_bucket_acl" "example" {
  depends_on = [
    aws_s3_bucket_ownership_controls.example,
    aws_s3_bucket_public_access_block.example,
  ]

  bucket = aws_s3_bucket.mybucket.id
  acl    = "public-read"
}

# Upload static files(HTML, CSS, JS, images,etc.) into our bucket to host the Static Website

# Uploading index.html file to our bucket
resource "aws_s3_object" "index" {
  bucket = aws_s3_bucket.mybucket.id
  key = "index.html"
  source = "index.html"
  acl = "public-read"
  content_type = "text/html"
}

# Uploading error.html file to our bucket
resource "aws_s3_object" "error" {
  bucket = aws_s3_bucket.mybucket.id
  key = "error.html"
  source = "error.html"
  acl = "public-read"
  content_type = "text/html"
}


# Enable Static Website Hosting Configuration to our bucket
resource "aws_s3_bucket_website_configuration" "website" {
  bucket = aws_s3_bucket.mybucket.id
  index_document {
    suffix = "index.html"
  }

  error_document {
    key = "error.html"
  }

  depends_on = [ aws_s3_bucket_acl.example ]
}
